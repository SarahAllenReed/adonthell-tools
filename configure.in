dnl **************************************************************
dnl Process this file with autoconf to produce a configure script.
dnl **************************************************************

AC_PREREQ(2.50)
AC_INIT(README)
AC_CONFIG_AUX_DIR(.)

dnl Detect the canonical host and target build environment
AC_CANONICAL_HOST
AC_CANONICAL_TARGET
AC_CONFIG_MACRO_DIR([m4])

AM_INIT_AUTOMAKE(adonthell-tools, 0.4.0-alpha-1)
AM_CONFIG_HEADER(config.h)

CXXFLAGS="-g -Wall -fno-exceptions"

dnl ********************
dnl Checks for programs.
dnl ********************

AC_PROG_LN_S
AC_PROG_CXX
AC_PROG_CXXCPP
AC_PROG_MAKE_SET
AC_PROG_RANLIB

dnl ****
dnl i18n
dnl ****

AM_GNU_GETTEXT([external])


dnl *********
dnl Endianess
dnl *********

AC_C_BIGENDIAN(CXXFLAGS="$CXXFLAGS -DBIG_ENDIAN", :, :)


dnl *******
dnl libtool
dnl *******

AC_LIBTOOL_DLOPEN
AC_LIBTOOL_WIN32_DLL
AC_PROG_LIBTOOL
AC_CHECK_LIB(ltdl, lt_dlinit,,echo "Adonthell requires libltdl. Exitting...";exit 1)


dnl *****************
dnl Adonthell
dnl *****************

PKG_CHECK_MODULES(ADONTHELL, [adonthell >= 0.4.0])
AC_SUBST(ADONTHELL_CFLAGS)
AC_SUBST(ADONTHELL_LIBS)

ADONTHELL_DATADIR=`pkg-config --variable=datadir adonthell`
ADONTHELL_BACKEND=`pkg-config --variable=libdir adonthell`
CXXFLAGS="$CXXFLAGS -DDATA_DIR=\\\"$ADONTHELL_DATADIR\\\""
AC_SUBST(ADONTHELL_BACKEND)


dnl *****************
dnl Cairo
dnl *****************

PKG_CHECK_MODULES(CAIRO, [cairo >= 1.2.6])
AC_SUBST(CAIRO_CFLAGS)
AC_SUBST(CAIRO_LIBS)


dnl *****************
dnl GTK+
dnl *****************

PKG_CHECK_MODULES(GTK, [gtk+-2.0 >= 2.16.0])
AC_SUBST(GTK_CFLAGS)
AC_SUBST(GTK_LIBS)


dnl *****************
dnl OSX Integration
dnl *****************

_gdk_tgt=`$PKG_CONFIG --variable=target gdk-2.0`
AM_CONDITIONAL([GDK_TARGET_QUARTZ], [test x$_gdk_tgt = xquartz])
if test "x$_gdk_tgt" = xquartz; then 
   PKG_CHECK_MODULES(IGE_MAC, ige-mac-integration)
   AC_SUBST(IGE_MAC_LIBS)
   AC_SUBST(IGE_MAC_CFLAGS)
fi


dnl *****************
dnl Check for libxml2
dnl *****************
XML_VERSION=2.5.0
AM_PATH_XML2($XML_VERSION,
    :,
    AC_MSG_ERROR([*** libxml version >= $XML_VERSION not found!])
)


dnl *****************
dnl GNU/Linux desktop integration
dnl *****************

AC_PATH_PROG(UPDATE_DESKTOP_DATABASE, update-desktop-database, no)
AC_PATH_PROG(UPDATE_MIME_DATABASE, update-mime-database, no)

AC_ARG_ENABLE(desktop-update, [AC_HELP_STRING(--disable-desktop-update, Disable the MIME desktop database update)], disable_desktop=yes, disable_desktop=no)
AC_ARG_ENABLE(mime-update, [AC_HELP_STRING(--disable-mime-update, Disable the MIME database update)], disable_mime=yes, disable_mime=no)

AM_CONDITIONAL(UPDATE_DESKTOP, test "x$UPDATE_DESKTOP_DATABASE" != "xno" -a "x$disable_desktop" = "xno")
AM_CONDITIONAL(UPDATE_MIME, test "x$UPDATE_MIME_DATABASE" != "xno" -a "x$disable_mime" = "xno")


dnl ****************
dnl Check for Python
dnl ****************

PY_CFLAGS=`pkg-config --variable=pycflags adonthell`
PY_LIBS=`pkg-config --variable=pylibs adonthell`
AC_SUBST(PY_CFLAGS)
AC_SUBST(PY_LIBS)


AC_OUTPUT([ 
Makefile
desktop/Makefile
src/Makefile
src/backend/Makefile
src/common/Makefile
src/dlgedit/Makefile
src/mapedit/Makefile
src/modeller/Makefile
src/questedit/Makefile
test/Makefile
util/Makefile
])
